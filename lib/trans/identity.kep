/**
 * @fileOverview Identity monad transformer.
 */
package Identity
with
    import '../structure' {Monad Monoid Transformer}
in {

/* Transform
 ******************************************************************************/
var runIdentityT := (.value);

/**
 * Identity monad transformer.
 * 
 * Transforms a monad to itself.
 * 
 * One application is to define a set of custom operations on a structure
 * without effecting the underlying structure. For example, specializing a
 * `State` on state type.
 * 
 * @param m Base monad.
 */
Identity := \m -> {
    var Instance := function \x =self-> {
        self.value = x;
    };
    
    Monad(Instance,
        (new, Instance) <\ m.of,
        
        \f =c->
            new Instance (
                runIdentityT(c)
                    .chain(f \> runIdentityT)));
    
    Monoid(Instance,
        new Instance m.zero,
        
        \b =a->
            new Instance (
                runIdentityT(a)
                    .concat (runIdentityT b)));
    
    Transformer(Instance, m,
        (new, Instance));
    
    return Instance;
};

/* Running
 ******************************************************************************/
/**
 * Run an identity computation.
 */
Identity.runIdentityT = runIdentityT;

}