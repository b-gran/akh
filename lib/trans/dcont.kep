/**
 * @fileOverview Delimited continuation monad transformer.
 * 
 * Based on:
 * http://www.cs.indiana.edu/~sabry/papers/monadicDC.pdf
 */
package DContT
with
    import 'nu-stream::stream' stream#{first rest isEmpty},
    
    import '../state' State,

    import '../structure' {Functor Monad}
in {

/* Records
 ******************************************************************************/
/**
 * Control segment.
 */
var Seg = function \f =self-> {
    self.frame = f;
};

/**
 * Functor control segment.
 */
var FSeg = function \f =self-> {
    self.frame = f;
};
FSeg.prototype = new Seg();

/**
 * Monad control segment.
 */
var MSeg = function \f =self-> {
    self.frame = f;
};
MSeg.prototype = new Seg();

/**
 * Delimiter.
 */
var P = function \t =self-> {
    self.prompt = t;
};

/* Control Stack
 ******************************************************************************/
var empty = stream.NIL;

var push = stream.cons;

/**
 * Push an entire slice of control stack onto a control stack.
 */
var pushSeq = stream.append;

/**
 * Push a delimiter `t` on onto control stack `k`.
 */
var pushP = \t, k ->
    push(new P(t), k);

/**
 * Push a functor segment for `f` onto control stack `k`.
 */
var pushFSeg = \f k ->
    push(new FSeg(f), k);

/**
 * Push a monad segment for `f` onto control stack `k`.
 */
var pushMSeg = \f k ->
    push(new MSeg(f), k);

/**
 * Splits the control stack around prompt `t`.
 */
var splitSeq = \t k -> {
    if (isEmpty k)
        return [empty, empty];
    
    var x = first k,
        xs = rest k;
    
    if (x instanceof P && x.prompt === t)
        return [empty, xs];
    
    var sub = splitSeq(t, xs);
    return [push(x, sub.(0)), sub.(1)];
};

/* Operations
 ******************************************************************************/
var unDContT = \m k ->
    m.run(k);

var runDContT = \m k ->
    State.evalState(unDContT(m, k), 1);

/**
 * Apply continuation `k`
 * 
 * @param k Continuation.
 * @param x Value
 */
var appk = \k x -> {
    var c = k;
    do {
        if (typeof c === 'function')
            return State.of <| c(x);

        var top := first c;
        if (top instanceof MSeg)
            return unDContT(top.frame(x), rest c);
        else if (top instanceof FSeg)
            return appk(rest c, top.frame(x));
        else
            c = ?top instanceof P 
                :rest c
                :top;
    } while (true);
};

/* Prompt
 ******************************************************************************/
var createPrompt = State.get
    .chain\x ->
        State.put(x + 1)
            .chain(\-> State.of x);

/* Transformer
 ******************************************************************************/
/**
 * Delimited continuation monad transformer.
 * 
 * @param m Base monad.
 */
DContT = \m -> {
    var Instance = function \run =self-> {
        self.run = run;
    };
    
    Functor(Instance,
        \c f ->
            new Instance\k ->
                unDContT(c, pushFSeg(f, k)));
    
    Monad(Instance,
        \x ->
            new Instance\k -> appk(k, x),

        \c f ->
            new Instance\k ->
                unDContT(c, pushMSeg(f, k)));
   
    // Lift
    Instance.lift = \t ->
        new Instance\k ->
            State.of <| t.chain(appk @ k);
    
    // Primitive Continuation Operations
    /**
     * Create a new unique prompt.
     */
    Instance.newPrompt = Instance.prototype.newPrompt = new Instance\k ->
        createPrompt.chain(appk @ k);
    
    /**
     * Pushes prompt onto the stack and evaluate `c`.
     * 
     * @param prompt Prompt.
     * @param c Computation.
     */
    Instance.pushPrompt = Instance.prototype.pushPrompt = \prompt c ->
        new Instance\k ->
            unDContT(c, pushP(prompt, k));
    
    /**
     * Capture the continuation delimited by `prompt` and call `f` with it.
     * 
     * @param prompt Prompt.
     * @param f Function mapping delimited continuation to computation.
     */
    Instance.withSubCont = Instance.prototype.withSubCont = \prompt f ->
        new Instance\k -> let
            sub = splitSeq(prompt, k)
        in
            unDContT(f(sub.(0)), sub.(1));
    
    /**
     * Push an entire sub continuation onto the stack and evaluate `c`.
     * 
     * @param subk Sub continuation.
     * @param c Computation
     */
    Instance.pushSubCont = Instance.prototype.pushSubCont = \subk c ->
        new Instance\k ->
            unDContT(c, pushSeq(subk, k));
    
    /**
     * Delimit a continuation
     * 
     * @param f Function taking a new prompt and returning the computation to be 
     *    enclosed.
     */
    Instance.reset = Instance.prototype.reset = \f->
        Instance.newPrompt
            .chain\p -> 
                Instance.pushPrompt(p, f(p));
    
    /**
     * Capture the continuation delimited by `p`
     * 
     * @param p Prompt.
     * @param f Function taking current delimited continuation.
     */
    Instance.shift = Instance.prototype.shift = \p f ->
        Instance.withSubCont(p, \k ->
            Instance.pushPrompt(p, f(\c ->
                Instance.pushPrompt(p, Instance.pushSubCont(k, c)))));
    
    return Instance;
};

/* Running
 ******************************************************************************/
/**
 * Perform a continuation computation and complete with `k`.
 * 
 * @param m ContT computation.
 * @param k Outer continuation.
 */
DContT.runDContT = \m k ->
    runDContT(m, push(k, empty));

}