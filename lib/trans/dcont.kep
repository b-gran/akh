/**
 * @fileOverview Delimited continuation monad transformer.
 * 
 * Based on:
 * http://www.cs.indiana.edu/~sabry/papers/monadicDC.pdf
 * 
 * @TODO: Tail call implementation could be improved.
 */
package DContT
with
    import 'nu-stream::stream' {append cons first rest isEmpty NIL},
    
    import './unique' UniqueT,
    import '../structure' {Monad Transformer},
    import '../_tail' {Tail trampoline}
in {

/* Segments
 ******************************************************************************/
/**
 * Control segment.
 */
var Seg = function \f =self-> {
    self.frame = f;
};

/**
 * Delimiter.
 */
var P = function \t =self-> {
    self.prompt = t;
};

/* Control Stack
 ******************************************************************************/
/**
 * Empty control stack.
 */
var empty = NIL;

/**
 * Push a single frame onto a control stack.
 */
var push = cons;

/**
 * Push an entire slice of control stack onto a control stack.
 */
var pushSeq = append;

/**
 * Push a delimiter `t` on onto control stack `k`.
 */
var pushP = \t, k ->
    push(new P(t), k);

/**
 * Push a segment for `f` onto control stack `k`.
 */
var pushSeg = \f k ->
    push(new Seg(f), k);

/**
 * Splits the control stack around prompt `t`.
 */
var splitSeq = \t k -> {
    if (isEmpty k)
        return [empty, empty];
    
    var x = first k,
        xs = rest k;
    
    if (x instanceof P && x.prompt === t)
        return [empty, xs];
    
    return let
        [a, b] = splitSeq(t, xs)
    in
        [push(x, a), b];
};

/* Interface
 ******************************************************************************/
var DContMonad = \instance newPrompt pushPrompt withSubCont pushSubCont -> {
    /**
     * M.newPrompt
     * 
     * Create a new unique prompt.
     */
    instance.newPrompt = instance.prototype.newPrompt = newPrompt;
    
    /**
     * M.pushPrompt(prompt, c)
     * 
     * Pushes prompt onto the stack and evaluate `c`.
     * 
     * @param prompt Prompt.
     * @param c Computation.
     */
    instance.pushPrompt = instance.prototype.pushPrompt = pushPrompt;
    
    /**
     * M.withSubCont(prompt, f)
     * 
     * Capture the continuation delimited by `prompt` and call `f` with it.
     * 
     * @param prompt Prompt.
     * @param f Function mapping delimited continuation to computation.
     */
    instance.withSubCont = instance.prototype.withSubCont = withSubCont;
    
    /**
     * M.pushSubCont(subk, c)
     * 
     * Push an entire sub continuation onto the stack and evaluate `c`.
     * 
     * @param subk Sub continuation.
     * @param c Computation
     */
    instance.pushSubCont = instance.prototype.pushSubCont = pushSubCont;
    
// Derived
    /**
     * M.reset(f)
     * 
     * Delimit a continuation
     * 
     * @param f Function taking a new prompt and returning the computation to be 
     *    enclosed.
     */
    instance.reset = instance.prototype.reset = \f->
        newPrompt
            .chain\p -> 
                pushPrompt(p, f p);
    
    /**
     * M.shift(p)
     * 
     * Capture the continuation delimited by `p`
     * 
     * @param p Prompt.
     * @param f Function taking current delimited continuation.
     */
    instance.shift = instance.prototype.shift = \p f =t->
        withSubCont(p, \k ->
            pushPrompt(p, f(\c ->
                pushPrompt(p, pushSubCont(k, c)))));
    
    return instance;
};


/* Transformer
 ******************************************************************************/
var unDContT = \m k ->
    new Tail(m.run, k);
    
var runDContT = unDContT \>> trampoline \> UniqueT.runUniqueT;

/**
 * Delimited continuation monad transformer.
 * 
 * @param m Base monad.
 */
DContT = \m -> {
    var M = UniqueT(m);
    
    var Instance = function \run =self-> {
        self.run = run;
    };
    
    /**
     * Apply continuation `k`
     * 
     * @param k Continuation.
     * @param x Value
     */
    var appk = \k x -> {
        var c = k;
        do {
            if (typeof c === 'function')
                return M.of <| c x;
            
            var top := first c;
            if (top instanceof Seg)
                return unDContT(top.frame(x), rest c);
            
            c = ?top instanceof P
                :rest c
                :top;
        } while (true);
    };
    
    Monad(Instance,
        \x ->
            new Instance\k ->
                appk(k, x),
        
        \f =c->
            new Instance\k ->
                unDContT(c, pushSeg(f, k)));

    Transformer(Instance, m,
        \t ->
            new Instance\k ->
                M.lift(t.map trampoline).chain(appk @ k \> trampoline));
    
    DContMonad(Instance,
        // newPrompt
        new Instance\k ->
            M.unique.chain(appk @ k \> trampoline),
        
        // pushPrompt;
        \prompt c ->
            new Instance\k ->
                unDContT(c, pushP(prompt, k)),
        
        // withSubCont;
        \prompt f ->
            new Instance\k -> let
                [x, xs] = splitSeq(prompt, k)
            in
                unDContT(f x, xs),
        
        // pushSubCont
        \subk c ->
            new Instance\k ->
                unDContT(c, pushSeq(subk, k)));
    
    return Instance;
};

/* Running
 ******************************************************************************/
/**
 * Perform a continuation computation and complete with `k`.
 * 
 * @param m ContT computation.
 * @param k Outer continuation.
 */
DContT.runDContT = \m k ->
    runDContT(m, push(k, empty));

}