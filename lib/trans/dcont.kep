/**
 * @fileOverview Delimited continuation monad transformer.
 * 
 * Based on:
 * http://www.cs.indiana.edu/~sabry/papers/monadicDC.pdf
 */
package DContT
with
    import 'nu-stream::stream' stream#{first rest isEmpty},
    
    import '../unique' Unique,
    import '../structure' {Monad},
    import '../_tail' {Tail trampoline}
in {

/* Records
 ******************************************************************************/
/**
 * Control segment.
 */
var Seg = function \f =self-> {
    self.frame = f;
};

/**
 * Delimiter.
 */
var P = function \t =self-> {
    self.prompt = t;
};

/* Control Stack
 ******************************************************************************/
var empty = stream.NIL;

var push = stream.cons;

/**
 * Push an entire slice of control stack onto a control stack.
 */
var pushSeq = stream.append;

/**
 * Push a delimiter `t` on onto control stack `k`.
 */
var pushP = \t, k ->
    push(new P(t), k);

/**
 * Push a segment for `f` onto control stack `k`.
 */
var pushSeg = \f k ->
    push(new Seg(f), k);

/**
 * Splits the control stack around prompt `t`.
 */
var splitSeq = \t k -> {
    if (isEmpty k)
        return [empty, empty];
    
    var x = first k,
        xs = rest k;
    
    if (x instanceof P && x.prompt === t)
        return [empty, xs];
    
    return let
        [a, b] = splitSeq(t, xs)
    in
        [push(x, a), b];
};

/* Operations
 ******************************************************************************/
var unDContT = \m k ->
    new Tail(m.run, k);

var runDContT = unDContT \>> trampoline \> Unique.runUnique;

/**
 * Apply continuation `k`
 * 
 * @param k Continuation.
 * @param x Value
 */
var appk = \k x -> {
    var c = k;
    do {
        if (typeof c === 'function')
            return Unique.of <| c x;

        var top := first c;
        if (top instanceof Seg)
            return unDContT(top.frame(x), rest c);

        c = ?top instanceof P 
            :rest c
            :top;
    } while (true);
};

/* Transformer
 ******************************************************************************/
/**
 * Delimited continuation monad transformer.
 * 
 * @param m Base monad.
 */
DContT = \m -> {
    var Instance = function \run =self-> {
        self.run = run;
    };
    
    Monad(Instance,
        \x ->
            new Instance\k ->
                appk(k, x),

        \c f ->
            new Instance\k ->
                unDContT(c, pushSeg(f, k)));
   
    // Lift
    Instance.lift = \t ->
        new Instance\k ->
            Unique.of <| t.chain(appk @ k);
    
    // Primitive Continuation Operations
    /**
     * Create a new unique prompt.
     */
    Instance.newPrompt = Instance.prototype.newPrompt = new Instance\k ->
        Unique.unique.chain(appk @ k \> trampoline );
    
    /**
     * Pushes prompt onto the stack and evaluate `c`.
     * 
     * @param prompt Prompt.
     * @param c Computation.
     */
    Instance.pushPrompt = Instance.prototype.pushPrompt = \prompt c ->
        new Instance\k ->
            unDContT(c, pushP(prompt, k));
    
    /**
     * Capture the continuation delimited by `prompt` and call `f` with it.
     * 
     * @param prompt Prompt.
     * @param f Function mapping delimited continuation to computation.
     */
    Instance.withSubCont = Instance.prototype.withSubCont = \prompt f ->
        new Instance\k -> let
            [x, xs] = splitSeq(prompt, k)
        in
            unDContT(f x, xs);
    
    /**
     * Push an entire sub continuation onto the stack and evaluate `c`.
     * 
     * @param subk Sub continuation.
     * @param c Computation
     */
    Instance.pushSubCont = Instance.prototype.pushSubCont = \subk c ->
        new Instance\k ->
            unDContT(c, pushSeq(subk, k));
    
    // Shift and Reset
    /**
     * Delimit a continuation
     * 
     * @param f Function taking a new prompt and returning the computation to be 
     *    enclosed.
     */
    Instance.reset = Instance.prototype.reset = \f->
        Instance.newPrompt
            .chain\p -> 
                Instance.pushPrompt(p, f p);
    
    /**
     * Capture the continuation delimited by `p`
     * 
     * @param p Prompt.
     * @param f Function taking current delimited continuation.
     */
    Instance.shift = Instance.prototype.shift = \p f =t->
        t.withSubCont(p, \k ->
            t.pushPrompt(p, f(\c ->
                t.pushPrompt(p, t.pushSubCont(k, c)))));
    
    return Instance;
};

/* Running
 ******************************************************************************/
/**
 * Perform a continuation computation and complete with `k`.
 * 
 * @param m ContT computation.
 * @param k Outer continuation.
 */
DContT.runDContT = \m k ->
    runDContT(m, push(k, empty));

}