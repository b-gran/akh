/**
 * @fileOverview Continuation monad transformer.
 */
package ContT
with
    import '../structure' {Functor Monad}
in {

/* Transformer
 ******************************************************************************/
/**
 * Continuation monad transformer.
 * 
 * @param m Base monad.
 */
ContT = \m -> {
    var Instance = function \run =self-> {
        self.run = run;
    };
    
    Monad(Instance,
        \x ->
            new Instance\k -> k(x),
        
        \c f ->
            new Instance\k ->
                ContT.runContT(c, \x ->
                    ContT.runContT(f x, k)));
    
    // Callcc
    Instance.callcc = Instance.prototype.callcc = let
        reify = \k ->
            \x ->
                new Instance\_ -> k x
    in
        \f ->
            new Instance\k ->
                ContT.runContT(
                    f <| reify k,
                    k);
    
    // Lift
    Instance.lift = \t ->
        new Instance\k ->
            t.chain(k);
    
    return Instance;
};

/* Running
 ******************************************************************************/
/**
 * Perform a continuation computation and complete with `k`.
 * 
 * @param m ContT computation.
 * @param k Outer continuation.
 */
ContT.runContT = \m k ->
    m.run(k);

}