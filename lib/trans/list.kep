/**
 * @fileOverview List monad transformer.
 * 
 * This uses Javascript arrays.
 */
package ListT
with
    import '../base' {liftM liftM2},
    import '../structure' {Functor Monoid Monad Transformer},
    import '../spec/state' StateMonad
in {

var foldr := \f z a -> Array.prototype.reduceRight.call(a, f, z);

var map := \f a -> Array.prototype.map.call(a, f);

var concat := Function.prototype.call.bind(Array.prototype.concat);

var flatten := Function.prototype.apply.bind(Array.prototype.concat, []);

var flip := \f -> \x y -> f(y, x);

var flattenM := liftM @ flatten;

/* Transformer
 ******************************************************************************/
var runListT := (.run);

/**
 * List monad transformer.
 * 
 * @param m Base monad.
 */
ListT := \m -> {
    var Instance := function \run =self-> {
        self.run = run;
    };

    var sequence := foldr @ (liftM2 @ (flip concat), m.of []);
    
    var mapM := map \>> sequence;
    
    Monoid(Instance,
        new Instance (
            m.of []),
        
        \b =a->
            new Instance (
                liftM2(concat,
                    runListT a,
                    runListT b)));
    
    Monad(Instance,
        \x ->
            new Instance (
                m.of [x]),
        
        \f =c->
            new Instance (
                runListT(c)
                    .chain(mapM @ (f \> runListT))
                    |> flattenM));
    
    Transformer(Instance, m,
        \t ->
            new Instance (
                liftM(\x -> [x], t)));
    
    StateMonad.tryLiftState(Instance, m);
    
    return Instance;
};

/* Running
 ******************************************************************************/
/**
 * Perform a list computation.
 * 
 * @param m ListT computation.
 */
ListT.runListT = runListT;

}